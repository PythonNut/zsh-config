#compdef dstat

local context state line
typeset -A opt_args

_get_dstat_plugins () {
    local update_policy
    zstyle -s ":completion:${curcontext}:" cache-policy update_policy
    if [[ -z "$update_policy" ]]; then
        zstyle ":completion:${curcontext}:" cache-policy \
          __caching_policy
    fi

    if ( [[ ${+_plugins} -eq 0 ]] || _cache_invalid dstat_plugins ) \
      && ! _retrieve_cache dstat_plugins; then
        _tail_offset="$(\
            dstat --list | grep "dstat:" -n | awk -F":" '{print $1}')"
        _plugins=(${=${${(f)"$(\
            dstat --list | tail -n "+$_tail_offset" | grep -v ":" | \
            sed "s/,/\n/g" | sed "/^[ \t]*$/d" | awk '{print "--"$1}'
        )"}}})
        _store_cache dstat_plugins _plugins
    fi

    local expl
    _wanted dstat_plugins expl 'dstat plugins' compadd -a _plugins
}

__caching_policy () {
    local -a oldp
    oldp=( "$1"(Nmw+1) )    # 1 week cache
    (( $#oldp ))
}

_arguments \
    {-h,--help}"[show usage]:" \
    "(-c --cpu)"{-c,--cpu}"[enable cpu stats]" \
    "-C['0,3,total' -> include cpu0, cpu3 and total cpu stats]:arg:" \
    "(-d --disk)"{-d,--disk}"[enable disk stats]" \
    "-D['total,hda' -> include hda and total disk stats]:arg:" \
    "(-g --page)"{-g,--page}"[enable page stats]" \
    "(-i --int)"{-i,--int}"[enable interrup stats]" \
    "-I['5,eth2' -> include int5 and interrup used by eth2]:arg:" \
    "(-l --load)"{-l,--load}"[enable load stats]" \
    "(-m --mem)"{-m,--mem}"[enable memory stats]" \
    "(-n --net)"{-n,--net}"[enable network stats]" \
    "-N['eth1,total' -> include eth1 and total network stats]:arg:" \
    "(-p --proc)"{-p,--proc}"[enable process stats]" \
    "(-r --io)"{-r,--io}"[enable io stats (I/O requests completed)]" \
    "(-s --swap)"{-s,--swap}"[enable swap stats]" \
    "-S['swap1,total' -> include swap1 and total swap stats]:arg:" \
    "(-t --time)"{-t,--time}"[enable time/date output]" \
    "(-T --epoch)"{-T,--epoch}"[enable time counter (seconds since epoch)]" \
    "(-y --sys)"{-y,--sys}"[enable system stats]" \
    "--aio[enable aio stats]" \
    "(-fs --filesystem)"{-fs,--filesystem}"[enable fs stats]" \
    "--ipc[enable ipc stats]" \
    "--lock[enable lock stats]" \
    "--raw[enable raw stats]" \
    "--socket[enable socket stats]" \
    "--tcp[enable tcp stats]" \
    "--udp[enable udp stats]" \
    "--unix[enable unix stats]" \
    "--vm[enable vm stats]" \
    "--list[list all available plugins]" \
    "(-a --all)"{-a,--all}"[equals -cdngy (default)]" \
    "(-f --full)"{-f,--full}"[automatically expand -C, -D, -I, -N and -S lists]" \
    "(-v --vmstat)"{-v,--vmstat}"[equals -pmgdsc -D total]" \
    "--float[force float values on screen]" \
    "--integer[force integer values on screen]" \
    "(-bw --blackonwhite)"{-bw,--blackonwhite}"[change colors for white background terminal]" \
    "--nocolor[disable colors (implies --noupdate)]" \
    "--noheaders[disable repetitive headers]" \
    "--noupdate[disable intermediate updates]" \
    "--output[write CSV output to file]:file:_gnu_generic" \
    "*::dstat plugins:_get_dstat_plugins"
